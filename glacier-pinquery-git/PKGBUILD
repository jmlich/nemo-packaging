# $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=glacier-pinquery-git
pkgver=0.3.5.r1.ge2acd51
_branch=qt6
pkgrel=1
pkgdesc="QML based PIN query application using libqofono"
arch=('x86_64' 'aarch64')
url="https://github.com/nemomobile-ux/glacier-pinquery"
license=('BSD-3-Clause')
# https://github.com/TelepathyIM/telepathy-qt/wiki/Connection-Managers#fragmentation-story-s01e03
depends=('qt6-glacier-app' 'libqofono-qt6')
makedepends=('git' 'cmake' 'qt6-tools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}#branch=$_branch")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
    cmake \
        -B "${pkgname}/build" \
        -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr'
    make -C "${pkgname}/build" all
}

package() {
    make -C "${srcdir}/${pkgname}/build" DESTDIR="$pkgdir" install

    mkdir -p ${pkgdir}/usr/lib/systemd/user/user-session.target.wants
    ln -s ../glacier-pinquery.service ${pkgdir}/usr/lib/systemd/user/user-session.target.wants/
}
