## $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=pulseaudio-module-keepalive
pkgver=1.0.1.r0.g3102ee5
pkgrel=3
pkgdesc="PulseAudio keepalive module"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/pulseaudio-module-keepalive"
license=('LGPLv2+')
depends=('libpulse' 'pulseaudio')
makedepends=('git' 'automake' 'autoconf' 'pulsecore-headers')
source=("${pkgname}::git+${url}" "0001-fix_build.patch")
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
    cd "${srcdir}/${pkgname}"
    patch -p1 --input="${srcdir}/0001-fix_build.patch"

}

build() {
  cd "${srcdir}/${pkgname}"
  echo 15.0 > .tarball-version
  ./bootstrap.sh
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
    make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
}
