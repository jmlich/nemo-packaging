# $Id$
# Maintainer: Chupligin Sergey (Neochapay) <neochapay@gmail.com>

_host="github.com"
_project=sailfishos
_basename=mapplauncherd
_branch=master

_gitname=$_basename
pkgname=$_basename-git

pkgver=4.2.7.r0.gfc1fad8

pkgrel=1
pkgdesc="Application launcher for fast startup"
arch=('x86_64' 'aarch64')
url="https://$_host/$_project/$_gitname#branch=$_branch"
license=('LGPL2.1')
depends=('systemd' 'dbus')
makedepends=('git' 'cmake' 'extra-cmake-modules')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}" '0001-sys-fs-cgroup-is-already-mounted-for-write-in-manjar.patch')
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}


prepare() {
    cd "${srcdir}/${pkgname}"
    patch -Np1 -i "${srcdir}/0001-sys-fs-cgroup-is-already-mounted-for-write-in-manjar.patch"
}


build() {
  cd "${srcdir}/${pkgname}"
  export BUILD_TESTS=1
  export MEEGO=1
  unset LD_AS_NEEDED
  rm -f CMakeCache.txt
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="$pkgdir" install
#enable booster service
  sed -i 's/WantedBy=user-session.target/WantedBy=graphical-session.target/' "${pkgdir}/usr/lib/systemd/user/booster-generic.service"
  mkdir -p $pkgdir/usr/lib/systemd/user/graphical-session.target.wants/
  ln -s ../booster-generic.service $pkgdir/usr/lib/systemd/user/graphical-session.target.wants/
#cgroup mount screept stuff
  install -D -m 0755 scripts/booster-cgroup-mount $pkgdir/usr/lib/startup/booster-cgroup-mount
  install -D -m 0755 rpm/booster-cgroup-mount.service $pkgdir/usr/lib/systemd/system/booster-cgroup-mount.service

  mkdir -p $pkgdir/usr/lib/systemd/system/multi-user.target.wants
  ln -s ../booster-cgroup-mount.service $pkgdir/usr/lib/systemd/system/multi-user.target.wants/
}

post_install() {
  /usr/bin/setcap cap_sys_ptrace+pe /usr/libexec/mapplauncherd/booster-generic || :
}