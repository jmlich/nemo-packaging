# $Id$

_basename=glacier-mail
pkgname=${_basename}-git
pkgver=0.0.2.r26.gfb55cc4
pkgrel=1
pkgdesc="Nemo mail client"
arch=('x86_64' 'aarch64')
url="https://github.com/nemomobile-ux/${_basename}"
license=('BSD-3-Clause' 'LGPL-2.1-only')
depends=('qt6-glacier-app' 'qt6-webengine' 'nemo-qml-plugin-email')
makedepends=('git' 'qt6-tools' 'clang')
provides=("${_basename}=${pkgver}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
    cmake \
        -B "${pkgname}/build" \
        -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr'
    make -C "${pkgname}/build" all
}

package() {
    make -C "${srcdir}/${pkgname}/build" DESTDIR="$pkgdir" install
}
