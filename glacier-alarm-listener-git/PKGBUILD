# $Id$
_basename=glacier-alarm-listener
pkgname=${_basename}-git
pkgver=0.2.r0.g4a86dab
pkgrel=2
pkgdesc="Nemo alarm events notification popup"
arch=('x86_64' 'aarch64')
url="https://github.com/nemomobile-ux/${_basename}"
license=('BSD-3-Clause')
depends=('qt6-glacier-app'
	'timed'
	'glacier-alarmclock')

makedepends=('git' 'cmake' 'qt6-tools' 'clang')
provides=("${_basename}=${pkgver}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
    cmake \
        -B "${pkgname}/build" \
        -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr'
    make -C "${pkgname}/build" all
}

package() {
    make -C "${srcdir}/${pkgname}/build" DESTDIR="$pkgdir" install
}
