# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libphonenumber-nemo
pkgver=8.13.8
pkgrel=1
epoch=1
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
url="https://github.com/googlei18n/libphonenumber"
arch=('x86_64' 'aarch64')
license=(Apache)
depends=('icu=72.1' protobuf abseil-cpp)
makedepends=(cmake gtest)
source=("https://github.com/google/libphonenumber/archive/refs/tags/v${pkgver}.tar.gz"
	"0001-Fix-geocoding-build-when-static-libraries-are-off.patch")
sha256sums=('b2a3723e0327d70c0f3b68496af306a1b8d5621a74f9aa6b871e528ac36ddf80'
            'f30e041e0030c483e1fcaa6ba365ea126473faa6aa11ca1bfd892393f68405db')
provides=("libphonenumber")

prepare() {
  cd libphonenumber-$pkgver
  sed -e 's|CMAKE_CXX_STANDARD 11|CMAKE_CXX_STANDARD 17|' -i tools/cpp/CMakeLists.txt # Fix build with abseil-cpp 2023
  patch -p1 --input="${srcdir}/0001-Fix-geocoding-build-when-static-libraries-are-off.patch"
}

build() {
  cd libphonenumber-$pkgver

  cmake -S cpp -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_STANDARD=17 \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DABSL_PROPAGATE_CXX_STD=ON \
    -DREGENERATE_METADATA=OFF \
    -DBUILD_STATIC_LIB=OFF \
    -DUSE_ICU_REGEXP=ON \
    -DUSE_LITE_METADATA=ON \
    -DUSE_RE2=OFF \
    -DUSE_PROTOBUF_LITE=ON \
    -DBUILD_GEOCODER=ON \
    -DUSE_BOOST=OFF \
    -DUSE_STDMUTEX=ON \
    -DBUILD_TESTING=OFF

  cmake --build build -v
}

package() {
  cd libphonenumber-$pkgver
  depends+=(libicu{uc,i18n}.so libprotobuf.so libboost_thread.so)
  provides+=(libgeocoding.so libphonenumber.so)

  DESTDIR="$pkgdir" cmake --install build
}
