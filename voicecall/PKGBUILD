# $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

_host="git.sailfishos.org"
_project=mer-core
_basename=voicecall
_branch=master

pkgname=$_basename-git

pkgver=0.7.11.r0.ga188e61

pkgrel=4
pkgdesc="Dialer engine for Nemo Mobile"
arch=('x86_64' 'aarch64')
url="https://$_host/$_project/$_basename#branch=$_branch"
license=('LGPL-2.1-only AND Apache-2.0')
depends=('mapplauncherd-qt' 'libpulse' 'nemo-qml-plugin-devicelock-git' 'qt5-resource-git' 'libqofono-qt5' 'qt5-multimedia' 'qt5-ngfd-git' 'telepathy-qt')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}" "0001-fix_build_on_new_qt.patch" '0002-Empty-implementation-of-some-functions.patch')
md5sums=('SKIP' 'SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -p1 --input "${srcdir}/0001-fix_build_on_new_qt.patch"
  patch -p1 --input "${srcdir}/0002-Empty-implementation-of-some-functions.patch"
}

build() {
  cd "${srcdir}/${pkgname}"
  qmake CONFIG+=enable-ngf CONFIG+=enable-audiopolicy CONFIG+=enable-telepathy CONFIG+=enable-nemo-devicelock CONFIG+=install-servicefiles
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make -j 1 INSTALL_ROOT="${pkgdir}" install

  sed -i 's/WantedBy=user-session.target/WantedBy=graphical-session.target/;s/pre-user-session.target/graphical-session-pre.target/' "${pkgdir}/usr/lib/systemd/user/voicecall-manager.service"
  mkdir -p ${pkgdir}/usr/lib/systemd/user/graphical-session.target.wants
  ln -s ../voicecall-manager.service ${pkgdir}/usr/lib/systemd/user/graphical-session.target.wants/

  mkdir -p ${pkgdir}/usr/share/mapplauncherd/privileges.d
  install -m 644 -p rpm/voicecall-qt5.privileges ${pkgdir}/usr/share/mapplauncherd/privileges.d/
}
