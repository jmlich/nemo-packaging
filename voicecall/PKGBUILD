# $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=voicecall
pkgver=0.8.2
pkgrel=1
pkgdesc="Dialer engine for Nemo Mobile"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/voicecall"
license=('LGPL-2.1-only AND Apache-2.0')
depends=('libpulse'
	 'nemo-qml-plugin-devicelock'
	 'libresourceqt'
	 'libqofono-qt6'
	 'qt6-multimedia'
	 'libngf-qt6'
	 'telepathy-ring'
	 'telepathy-farstream'
	 'telepathy-qt')
makedepends=('cmake')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
	'voicecall-manager.service'
	'0001-Allow-build-with-qt6.patch'
)
sha256sums=('10b31a14bc93df68d8d16373adffffe5ae3d7b9dcdcacf3f65822d44a1e28a3e'
    '61c9da6be16743334d608f19a7a7cd3d104264e3409f212078b6a60c1d628dce'
    '177b951d715110e0d769301cbcbc6aa695d113fe81421ccf3a4a65e246802815'

)

prepare() {
    cd $pkgname-$pkgver
    patch -p1 --input="${srcdir}/0001-Allow-build-with-qt6.patch"
}


build() {
    cd $pkgname-$pkgver
    qmake6 CONFIG+=enable-ngf \
	CONFIG+=enable-audiopolicy \
	CONFIG+=enable-telepathy \
	CONFIG+=enable-nemo-devicelock \
	CONFIG+=install-servicefiles
    make
}

package() {
    cd $pkgname-$pkgver
    make -j 1 INSTALL_ROOT="${pkgdir}" install

    rm "${pkgdir}/usr/lib/systemd/user/voicecall-manager.service"
    cp ${srcdir}/voicecall-manager.service "${pkgdir}/usr/lib/systemd/user/voicecall-manager.service"
    mkdir -p ${pkgdir}/usr/lib/systemd/user/user-session.target.wants
    ln -s ../voicecall-manager.service ${pkgdir}/usr/lib/systemd/user/user-session.target.wants/
}
