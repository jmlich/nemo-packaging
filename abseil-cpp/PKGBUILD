# $Id$

pkgname=abseil-cpp
pkgver=20230125.1
pkgrel=1
pkgdesc="Abseil is an open-source collection of C++ code"
arch=('x86_64' 'aarch64')
url="https://github.com/abseil/abseil-cpp"

license=('Apache-2.0 License')
makedepends=('git' 'cmake')
source=("${url}/archive/$pkgver/abseil-cpp-$pkgver.tar.gz"
    '0001-Add-fPIC.patch'
)
sha256sums=('81311c17599b3712069ded20cca09a62ab0bf2a89dfa16993786c8782b7ed145'
    '917df057c253c0edf5710ee21ed4dac8991eb3e1e8c447f48ea05de49e98b384'
)

prepare() {
    cd "$srcdir/${pkgname}-${pkgver}"
    patch -p1 --input="${srcdir}/0001-Add-fPIC.patch"
}

build() {
    cmake \
        -B "${pkgname}-${pkgver}/build" \
        -S "${pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr'
    make -C "${pkgname}-${pkgver}/build" all
}

package() {
    make -C "${srcdir}/${pkgname}-${pkgver}/build" DESTDIR="$pkgdir" install
}
