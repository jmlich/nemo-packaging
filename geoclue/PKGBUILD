# Maintainer: Jan Alexander Steffens (heftig) <hef...@archlinux.org>
# Contributor: György Balló <ball...@freestart.hu>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=geoclue
pkgver=2.5.7
pkgrel=5
pkgdesc="Modular geoinformation service built on the D-Bus messaging system"
arch=('x86_64' 'aarch64')
url="https://www.freedesktop.org/wiki/Software/GeoClue/";
license=(LGPL)
depends=(libsoup json-glib libmm-glib avahi geocode-glib)
makedepends=(systemd gobject-introspection git vala meson gtk-doc libnotify)
optdepends=('libnotify: Demo Agent')
provides=("geoclue2=$pkgver-$pkgrel")
conflicts=(geoclue2)
replaces=(geoclue2)
backup=(etc/geoclue/geoclue.conf)
source=("git+https://gitlab.freedesktop.org/geoclue/geoclue.git")
sha256sums=('SKIP')


build() {
  arch-meson $pkgname build \
    -D dbus-srv-user=geoclue \
    -D dbus-sys-dir=/usr/share/dbus-1/system.d
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  echo 'u geoclue - "Geoinformation service" /var/lib/geoclue' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

  echo 'd /var/lib/geoclue 0755 geoclue geoclue' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}

# vim:set sw=2 et: