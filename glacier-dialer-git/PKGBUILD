# Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

_host="github.com"
_project=nemomobile-ux
_basename=glacier-dialer
_branch=master

_gitname=$_basename
pkgname=$_basename-git

pkgver=0.2.1.r9.g4d408ac

pkgrel=1
pkgdesc="QML based dialer application for nemomobile"
arch=('x86_64' 'aarch64')
url="https://$_host/$_project/$_gitname"
license=('BSD-3-Clause')
depends=('qt5-glacier-app-git' 'nemo-qml-plugin-contacts-git' 'voicecall' 'commhistory-daemon-git' 'libcommhistory-git' 'libqofono' 'mapplauncherd-booster-qtcomponents-git' 'nemo-qml-plugin-dbus')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
  mkdir -p "${srcdir}/${pkgname}/build"
  cd "${srcdir}/${pkgname}/build"
  cmake -DCMAKE_INSTALL_PREFIX="$pkgdir"/usr ..
  make
}

package() {
  cd "${srcdir}/${pkgname}/build"
  make INSTALL_ROOT="${pkgdir}" install
 
  mkdir -p ${pkgdir}/usr/lib/systemd/user/user-session.target.wants
  ln -s ../voicecall-ui-prestart.service ${pkgdir}/usr/lib/systemd/user/user-session.target.wants/

  mkdir -p ${pkgdir}/usr/share/themes/glacier/meegotouch/icons/
  cd ${pkgdir}/usr/share/themes/glacier/meegotouch/icons/
  ln -s /usr/share/glacier-dialer/images/glacier-dialer.png icon-lock-missed-call.png
}