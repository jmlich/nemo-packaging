# Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=glacier-dialer-git
pkgver=0.5.4.r9.gc1a5bb7
pkgrel=1
pkgdesc="QML based dialer application for nemomobile"
arch=('x86_64' 'aarch64')
url="https://github.com/nemomobile-ux/glacier-dialer"
license=('BSD-3-Clause')
depends=('qt6-glacier-app'
    'nemo-qml-plugin-contacts'
    'voicecall'
    'libcommhistory'
    'libqofono-qt6'
    'nemo-qml-plugin-dbus')

makedepends=('git' 'cmake' 'qt6-tools' 'clang')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
    cmake \
        -B "${pkgname}/build" \
        -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr'
    make -C "${pkgname}/build" all
}

package() {
    make -C "${srcdir}/${pkgname}/build" DESTDIR="$pkgdir" install

    mkdir -p ${pkgdir}/usr/lib/systemd/user/user-session.target.wants
    ln -s ../voicecall-ui-prestart.service ${pkgdir}/usr/lib/systemd/user/user-session.target.wants/

    mkdir -p ${pkgdir}/usr/share/themes/glacier/meegotouch/icons/
    cd ${pkgdir}/usr/share/themes/glacier/meegotouch/icons/
    ln -s /usr/share/glacier-dialer/images/glacier-dialer.png icon-lock-missed-call.png
}
