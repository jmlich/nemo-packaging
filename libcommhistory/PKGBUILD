# $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=libcommhistory
pkgver=1.12.1
pkgrel=3
pkgdesc="Communications event history database API"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/libcommhistory"
license=('LGPL-2.1-only AND Apache-2.0')
depends=('libphonenumber-nemo'
    'qt6-base'
    'qtcontacts-sqlite'
    'mlite6'
    'nemo-qml-plugin-contacts')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
    "0001-allow_build_with_qt6.patch")
sha256sums=('c0b9f25bcc2dc6656e8fa4be10339db2db5f5ab1e417792a629b3593028b3a7f'
    '18e2bbd1901e54ef1c92c2afe62163497c023fac2b87ee3d18ccb7f44767cd67')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 --input="${srcdir}/0001-allow_build_with_qt6.patch"
}

build() {
  cd $pkgname-$pkgver
  mkdir -p build
  cd build
  qmake6 .. PKGCONFIG_LIB=/usr/lib PROJECT_VERSION=$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  cd build
  touch ../tests/tests.xml
  touch ../tests/performance_tests.xml
  make -j 1 INSTALL_ROOT="${pkgdir}" install
  rm -rf ${pkgdir}/opt
}
