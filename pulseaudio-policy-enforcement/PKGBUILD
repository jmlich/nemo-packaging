## $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=pulseaudio-policy-enforcement
pkgver=14.2.44.r0.g6bf81c4
pkgrel=2
pkgdesc="Pulseaudio module for enforcing policy decisions in the audio domain"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/pulseaudio-policy-enforcement"
license=('LGPLv2+')
depends=('libpulse' 'pulseaudio')
makedepends=('git' 'automake' 'autoconf' 'pulsecore-headers' 'pulseaudio-modules-nemo' 'libatomic_ops')
source=("${pkgname}::git+${url}" "0001-fix_build.patch")
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
    cd "${srcdir}/${pkgname}"
    patch -p1 --input="${srcdir}/0001-fix_build.patch"

}

build() {
  cd "${srcdir}/${pkgname}"
  echo 15.0 > .tarball-version
  ./bootstrap.sh
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --with-module-dir=/usr/lib/pulse-15.0/modules \
    --disable-static
    make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
}
