# $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=libsocialcache
pkgver=0.1.7.r0.g6c3f563
pkgrel=1
pkgdesc="A library that manages data from social networks"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/libsocialcache"
license=('BSD-3-Clause')
depends=('buteo-syncfw')

makedepends=('git' 'qt5-tools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}" "0001-use_mlite_for_keys.patch")
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
    cd "${srcdir}/${pkgname}"
    patch -p1 --input="${srcdir}/0001-use_mlite_for_keys.patch"
}

build() {
  cd "${srcdir}/${pkgname}"
  qmake-qt5
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make -j 1 INSTALL_ROOT="${pkgdir}" install
  # Remove installed tests
  rm -r "$pkgdir"/opt
}
