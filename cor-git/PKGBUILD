# $Id$
# Maintainer: TheKit <nekit1000 at gmail.com>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=cor-git
pkgver=0.1.21.r0.g9b06f7b
pkgrel=1
pkgdesc="another C++/C library. Contains useful utilities which are not a part of any other library"
arch=('x86_64' 'aarch64')
url="https://git.sailfishos.org/mer-core/cor"
license=('GPL')
depends=('systemd-libs' 'libudev0-shim')
makedepends=('git' 'cmake' 'check' 'tut' 'systemd' 'systemd-libs')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://git.sailfishos.org/mer-core/cor.git' '0001-avoid-warning-Compatibility-with-CMake-2.8.12-will-b.patch')
md5sums=('SKIP' 'SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	patch -p1 --input="${srcdir}/0001-avoid-warning-Compatibility-with-CMake-2.8.12-will-b.patch"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DVERSION=$pkgver ..
	make
}

package() {
	cd "$srcdir/${pkgname%-git}/build"
	make DESTDIR="$pkgdir/" install
        # Remove tests
        rm -rf "$pkgdir"/opt
}
 
