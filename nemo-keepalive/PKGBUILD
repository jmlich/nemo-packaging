## $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=nemo-keepalive
pkgver=1.8.9
pkgrel=1
pkgdesc="CPU and display keepalive and scheduling library"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/nemo-keepalive"
license=('LGPL-2.1-or-later')
depends=('qt6-declarative' 'libiphb' 'mce-headers')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
    '0001-Fix-use-with-Qt6.patch'
)
sha256sums=('120478bf093e9c829699be84b314d899a8a7ab446c3f060abea4b0048432750f'
    '9905c19dc5e12d30b60ab799a1a178ccad5b6ffec05e6ef9163c318829d6f49b'
)


prepare() {
  cd $pkgname-$pkgver
  patch -p1 --input="${srcdir}/0001-Fix-use-with-Qt6.patch"
}

build() {
    cd $pkgname-$pkgver
    export VERSION=`echo ${pkgver} | sed 's/+.*//'`
    qmake6 VERSION=${VERSION}
    make
}

package() {
    cd $pkgname-$pkgver
    mkdir -p $pkgdir/usr/lib/pkgconfig/
    make -j 1 INSTALL_ROOT="$pkgdir/" install

    # Remove tests
    rm -rf "$pkgdir"/opt

}
