## $Id$
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev (aa13q) <aa13q@ya.ru>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=nemo-qml-plugin-systemsettings
pkgver=0.5.89.1
pkgrel=1
pkgdesc="System settings plugin for Nemo Mobile"
arch=('x86_64' 'aarch64')
url="https://github.com/nemomobile-ux/nemo-qml-plugin-systemsettings"
license=('BSD')
depends=('qt5-systems'
	'libusb-moded-qt'
	'profiled'
	'timed'
	'nemo-qml-plugin-models'
	'libconnman-qt'
	'mlite'
	'mce'
	'nemo-qml-plugin-dbus'
	'qt5-xmlpatterns')

makedepends=('qt5-tools' 'mce-headers')
source=("${url}/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('8e3deb49ea9a195a4667c8b8b1b3138570915c148f872506a769f8408bdf39a6')

build() {
    cd $pkgname-$pkgver
    qmake
    make
}

package() {
    cd $pkgname-$pkgver
    make -j 1 INSTALL_ROOT="$pkgdir/" install
    cd "$pkgdir"
    rm -rf opt
    mkdir -p usr/lib/${pkgname}
    mv usr/libexec/* usr/lib/${pkgname}
    rm -rf usr/libexec
}
