# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libphonenumber
pkgver=8.13.4
pkgrel=2
epoch=1
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
url="https://github.com/googlei18n/libphonenumber"
arch=(x86_64)
license=(Apache)
depends=('icu=72.1' protobuf abseil-cpp)
makedepends=(cmake gtest git)
source=("https://github.com/google/libphonenumber/archive/refs/tags/v${pkgver}.tar.gz"
	"0001-Fix-geocoding-build-when-static-libraries-are-off.patch")
sha256sums=('7c9d1b7bc3320e1a234bdd9435486bc01fca181d74455e2866af19543289ae6c'
            'f30e041e0030c483e1fcaa6ba365ea126473faa6aa11ca1bfd892393f68405db')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 --input="${srcdir}/0001-Fix-geocoding-build-when-static-libraries-are-off.patch"
}

build() {
  cd $pkgname-$pkgver

  cmake -S cpp -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_STANDARD=17 \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DABSL_PROPAGATE_CXX_STD=ON \
    -DREGENERATE_METADATA=OFF \
    -DBUILD_STATIC_LIB=OFF \
    -DUSE_ICU_REGEXP=ON \
    -DUSE_LITE_METADATA=ON \
    -DUSE_RE2=OFF \
    -DUSE_PROTOBUF_LITE=ON \
    -DBUILD_GEOCODER=ON \
    -DUSE_BOOST=OFF \
    -DUSE_STDMUTEX=ON \
    -DBUILD_TESTING=OFF

  cmake --build build -v
}

package() {
  cd $pkgname-$pkgver
  depends+=(libicu{uc,i18n}.so libprotobuf.so libboost_thread.so)
  provides+=(libgeocoding.so libphonenumber.so)

  DESTDIR="$pkgdir" cmake --install build
}
