# $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

_host="github.com"
_project=nemomobile-ux
_basename=mapplauncherd-booster-qtcomponents
_branch=nemo

pkgname=$_basename-git

pkgver=0.2.0.r2.g22a307e

pkgrel=1
pkgdesc="Application launch boosters for Qt5 nemomobile components"
arch=('x86_64' 'aarch64')
url="https://$_host/$_project/$_basename#branch=$_branch"
license=('LGPL-2.1-only AND Apache-2.0')
depends=('mapplauncherd' 'qt5-base' 'mapplauncherd-qt' 'qt5-declarative')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
  cd "${srcdir}/${pkgname}"
}

build() {
  cd "${srcdir}/${pkgname}"
  qmake
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make -j 1 INSTALL_ROOT="${pkgdir}" install
  sed -i 's/pre-user-session.target/graphical-session-pre.target/;s/WantedBy=user-session.target/WantedBy=graphical-session.target/' "${pkgdir}/usr/lib/systemd/user/booster-nemomobile.service"

}
