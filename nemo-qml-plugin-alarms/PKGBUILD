## $Id$

pkgname=nemo-qml-plugin-alarms
pkgver=0.3.12
pkgrel=1
pkgdesc="alarms plugin for Nemo Mobile"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/nemo-qml-plugin-alarms"
license=('BSD')
depends=('qt6-base' 'timed' 'qt6-declarative')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
    '0001-Fix-build-with-qt6.patch'
    '0002-distinguish-wakeup-type-in-AlarmObject.patch'
)
sha256sums=('8435d73b25e28f75e5bffc9aba6d7d0fab56253596a4f0340d26428fd45edd3b'
    '6917bd1afc83af1c5c5a7d0e8f6d565c505855de037bbd4798e5e5b19ee8d2c6'
    'b8e94c842fe3050ef742c69b5d1967d70e45fa4f308e3b9e185c5775a64a3e5d'
)

prepare() {
  cd $pkgname-$pkgver
  patch -p1 --input="${srcdir}/0001-Fix-build-with-qt6.patch"
  patch -p1 --input="${srcdir}/0002-distinguish-wakeup-type-in-AlarmObject.patch"
}

build() {
    cd $pkgname-$pkgver
    qmake6
    make
}

package() {
    cd $pkgname-$pkgver
    make -j 1 INSTALL_ROOT="$pkgdir/" install
    cd "$pkgdir"
    rm -rf opt
}
