# Maintainer: Bernhard Landauer <bernhard@manjaro.org>

pkgname=nemo-qml-plugin-dbus
pkgver=2.1.31
pkgrel=2
pkgdesc="Nemo Mobile D-Bus QML Plugin."
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/nemo-qml-plugin-dbus"
license=('BSD' 'LGPL2.1')
depends=('qt5-base' 'qt5-declarative')
provides=('libnemodbus.so=2')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('d964c9eeb46e0ed3521fa0d5401e3aa487106a366871a29601ed2935e33fd637')

build() {
  cd "$pkgname-$pkgver"
  qmake "VERSION=$pkgver" src
  make
}

package() {
  cd "$pkgname-$pkgver"
  make INSTALL_ROOT="${pkgdir}" install

  # org.nemomobile.dbus legacy import
  install -d "${pkgdir}"/usr/lib/qt/qml/org/nemomobile/dbus/
  ln -sf /usr/lib/qt/qml/Nemo/DBus/libnemodbus.so \
    "${pkgdir}"/usr/lib/qt/qml/org/nemomobile/dbus/
  sed 's/Nemo.DBus/org.nemomobile.dbus/' \
    < "${pkgdir}"/usr/lib/qt/qml/Nemo/DBus/qmldir \
      > "${pkgdir}"/usr/lib/qt/qml/org/nemomobile/dbus/qmldir

  install -Dm644 license.bsd -t "${pkgdir}/usr/share/licenses/$pkgname/"
}
