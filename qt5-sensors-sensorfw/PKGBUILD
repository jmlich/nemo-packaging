# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt6-sensors-sensorfw
pkgver=6.8.2
pkgrel=1
arch=('x86_64' 'aarch64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='SensorFW plugin for qt6-sensors'
depends=('sensorfw>=0.14.7' 'qt6-sensors')
makedepends=('qt6-declarative' 'cmake' 'git')
optdepends=('qt6-declarative: QML bindings')
groups=('qt' 'qt6')
source=("https://github.com/qt/qtsensors/archive/refs/tags/v${pkgver}.tar.gz"
    '2001-convert-sensorfw-plugin-into-a-standalone-plugin.patch'
)
sha256sums=('b4b3da4e05964e7998a8eb2cc7d570a68feccb4a71d0c01683665a21e928802d'
    '71d42ab07c7dff329f9bd156e0fc5b154097aab1f3caa095734a3c021a433814'
)

prepare() {
    cd qtsensors-${pkgver}
    patch -p1 --input="${srcdir}/2001-convert-sensorfw-plugin-into-a-standalone-plugin.patch"
}

build() {
  cd qtsensors-${pkgver}/src/plugins/sensors/sensorfw
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd qtsensors-${pkgver}/src/plugins/sensors/sensorfw
  make -j 1 DESTDIR="${pkgdir}" install
}
