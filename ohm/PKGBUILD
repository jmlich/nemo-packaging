## $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=ohm
_srcname=ohm
_branch=master
pkgver=1.3.0.r0.gf62dee0
pkgrel=1
pkgdesc="Open Hardware Manager"
arch=('x86_64' 'aarch64')
url="https://git.sailfishos.org/mer-core/ohm.git"
license=('LGPL-2.1-only')
depends=('glib2' 'libtrace-ohm' 'dbus-glib')
makedepends=('git' 'automake' 'autoconf' 'check')
optdepends=()
provides=("${_srcname}")
conflicts=()
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
    cd "${srcdir}/${pkgname}"
    ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static \
    --without-xauth \
    --with-distro=meego
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
}
 
