## $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=ohm
pkgver=1.3.1
pkgrel=2
pkgdesc="Open Hardware Manager"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/ohm"
license=('LGPL-2.1-only')
depends=('glib2' 'libtrace-ohm' 'dbus-glib' 'python')
makedepends=('automake' 'autoconf' 'check')
source=("${url}/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('bc2b93686721461a282d61fc6bd2203e8ab65d401d56e6ebc06c759b6d84e63b')

prepare() {
    cd $pkgname-$pkgver
    echo -n "${pkgver}" > .tarball-version
    ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static \
    --without-xauth \
    --with-distro=meego
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install

  install -d ${pkgdir}/usr/lib/systemd/system/basic.target.wants/
  ln -s ../ohmd.service $pkgdir/usr/lib/systemd/system/basic.target.wants/
  ln -s ohmd.service $pkgdir/usr/lib/systemd/system/dbus-org.freedesktop.ohm.service

  install -d ${pkgdir}/var/lib/ohm

#modules config provide by glacier-wayland-session package
  rm -rf ${pkgdir}/etc/ohm/modules.ini
  
#dont add debug config into package
  rm -rf ${pkgdir}/etc/sysconfig/ohmd.debug
}
