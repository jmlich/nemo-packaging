# $Id$
pkgname=cutiepi-cutoff-git
pkgver=r28.73d056a
pkgrel=1
pkgdesc="Cutiepie Cutoff - send power off to hardware on shutdown"
arch=('x86_64' 'aarch64')
url='https://github.com/cutiepi-io/cutiepi-middleware'
license=('GPL')
depends=('qt5-base' 'qt5-serialport')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
    mkdir -p "$pkgname/cutoff/build"
    cd "$pkgname/cutoff/build"
    qmake-qt5 ..
    make
}

package() {
    mkdir -p ${pkgdir}/usr/lib/systemd/system-shutdown/
    install -m 755 ${pkgname}/cutoff/build/cutoff ${pkgdir}/usr/lib/systemd/system-shutdown/
}
