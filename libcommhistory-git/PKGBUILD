# $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=libcommhistory-git
pkgver=1.11.12.r0.gd684a51
pkgrel=1
pkgdesc="Nemomobile Qt Quick Controls"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/libcommhistory"
license=('LGPL-2.1-only AND Apache-2.0')
depends=('libphonenumber' 'qt5-base' 'qtcontacts-sqlite' 'nemo-qml-plugin-contacts-git')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}" "0001-fix-warnings-and-api.patch")
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -p1 --input="${srcdir}/0001-fix-warnings-and-api.patch"
}

build() {
  cd "${srcdir}/${pkgname}"
  mkdir -p build
  cd build
  qmake .. PKGCONFIG_LIB=lib
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  cd build
  make -j 1 INSTALL_ROOT="${pkgdir}" install
  mkdir -p ${pkgdir}/usr/share/mapplauncherd/privileges.d
  install -m 644 -p ${srcdir}/${pkgname}/rpm/libcommhistory-qt5.privileges ${pkgdir}/usr/share/mapplauncherd/privileges.d
}
