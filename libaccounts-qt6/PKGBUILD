# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libaccounts-qt6
pkgdesc='Qt-based client library for accessing the online accounts database'
pkgver=1.16
pkgrel=1
arch=(x86_64)
url='https://gitlab.com/accounts-sso/libaccounts-qt'
license=(LGPL)
depends=(libaccounts-glib qt6-base)
makedepends=(doxygen)
source=(https://gitlab.com/accounts-sso/libaccounts-qt/-/archive/VERSION_$pkgver/libaccounts-qt-VERSION_$pkgver.tar.gz
    '96ec99572d93f95279ff54c504576fb6741824d4.diff'
    'c86db79be389601bf6798f815ca5b8f78a524156.diff'
    '0001-allow_build_with_qt6.patch')
sha256sums=('3e527b151ee87e851b0f027214bda1353d8e48e10a5b54109f1e4b7d422a7de3'
    '8220acb6be8b97a3a1f49ccf31af9a137365d10062be92f33758999e00944586'
    '0f7873b6c3b2f9575831f9c23598dec357da937b0ff514afd5bf6151905f2202'
    'e7d2cbdca2390d1aac72d994be92765bc0545d2ebf37d3875efa0dfeb43ffa69')

prepare() {
  cd libaccounts-qt-VERSION_$pkgver
  patch -p1 --input="${srcdir}/96ec99572d93f95279ff54c504576fb6741824d4.diff"
  patch -p1 --input="${srcdir}/c86db79be389601bf6798f815ca5b8f78a524156.diff"
  patch -p1 --input="${srcdir}/0001-allow_build_with_qt6.patch"
  sed -i 's|SUBDIRS  += Accounts tests|SUBDIRS += Accounts|' accounts-qt.pro
}

build() {
  cd libaccounts-qt-VERSION_$pkgver
  qmake6 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd libaccounts-qt-VERSION_$pkgver
  make INSTALL_ROOT="$pkgdir" install

# Remove docs
  rm -r "$pkgdir"/usr/share
}