# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

_host="git.sailfishos.org"
_project=mer-core
_basename=contactsd
_branch=master

_gitname=$_basename
pkgname=$_basename-git

pkgver=1.4.9.r0.g19a0201

pkgrel=1
pkgdesc="Telepathy <> QtContacts bridge for contacts"
arch=('x86_64' 'aarch64')
url="https://$_host/$_project/$_gitname#branch=$_branch"
license=('BSD-3-Clause')
depends=('telepathy-qt' 'qt5-mlite-git' 'qt5-mlocale-git' 'libqofono-qt5' 'mapplauncherd-qt-git' 'qt5-contacts-sqlite-extensions-git' 'buteo-syncfw' 'kcalendarcore' 'mkcal-git' 'nemo-qml-plugin-contacts-git' 'qt5-ofono-nemo-extensions-git')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}" "0001-KcalendarCore_not_provide_pc_files.path")
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -p1 --input="${srcdir}/0001-KcalendarCore_not_provide_pc_files.path"
}

build() {
  cd "${srcdir}/${pkgname}"
  ./configure
  qmake
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make INSTALL_ROOT="${pkgdir}" install
}