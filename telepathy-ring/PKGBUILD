# Maintainer: Dan Johansen <strit@manjaro.org>
# Maintainer: Furkan Kardame <furkan@fkardame.com>

pkgname=telepathy-ring
pkgver=2.5.13
pkgrel=2
pkgdesc='GSM connection manager for the Telepathy framework'
arch=('aarch64' 'x86_64')
url="https://github.com/sailfishos/$pkgname"
license=('LGPL')
depends=('ofono' 'libofono-qt' 'qt6-feedback' 'python')
makedepends=('autoconf' 'automake'  'libxslt'
		'qt6-base'
		'libphonenumber-nemo'
		'telepathy-qt'
		'sqlite'
		'libngf'
		'libdbusaccess'
		'telepathy-mission-control'
		'libofono-qt'
		'check')

source=("${url}/archive/refs/tags/$pkgver.tar.gz"
	"telepathy-ring.service"
	"0001-fixup_access.patch"
	"0002-fixup_connection.patch")
sha256sums=('54a781ea3ead7e1c7025e589e83761fa932d0d595f323fde7cbd5c041869f5da'
	    '3d0f29f35114cf009e83219a870afdb571653949c8a5025a754e8f636686e86e'
	    '7e2918685655d0d09a9233983a5862219768e071330cbb693a360b84f4ee0d5e'
	    'b27c5933a659f40ec5b06e33f3f176d505e431d42dd4342d5fb0bc450134fd9b')

prepare() {
    cd $pkgname-$pkgver
    patch -p1 --input="rpm/0001-python3-changes.patch"
    patch -p1 --input="${srcdir}/0001-fixup_access.patch"
    patch -p1 --input="${srcdir}/0002-fixup_connection.patch"
}

build() {
	cd $pkgname-$pkgver
	mkdir m4 || true
	./autogen.sh --no-configure
	./configure --prefix=/usr \
	--libexecdir=/usr/bin \
	--enable-debug
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
#remove tests
	rm -rf $pkgdir/opt
#remove service and replace to own
	rm ${pkgdir}/usr/lib/systemd/user/telepathy-ring.service
	cp ${srcdir}/telepathy-ring.service ${pkgdir}/usr/lib/systemd/user/
#remove other stuff
	rm ${pkgdir}/usr/lib/*.a
}
