# $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

_basename=glacier-settings
pkgname=${_basename}-git
pkgver=0.8.10.r0.gd5869f1
pkgrel=1
pkgdesc="QML based settings application for nemomobile"
arch=('x86_64' 'aarch64')
url="https://github.com/nemomobile-ux/${_basename}"
license=('BSD-3-Clause')
depends=('qt6-glacier-app'
	'qt6-positioning'
	'nemo-qml-plugin-devicelock'
	'nemo-qml-plugin-settings'
	'libconnman-qt'
	'libmce-qt'
	'bluez-qt>=6.0'
	'nemo-qml-plugin-connectivity'
	'nemo-qml-plugin-configuration'
	'nemo-qml-plugin-models>=0.2.1'
	'nemo-qml-plugin-systemsettings>=0.8.0')
makedepends=('git' 'qt6-tools' 'clang' 'cmake')
provides=("${_basename}=${pkgver}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
    cmake \
        -B "${pkgname}/build" \
        -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr'
    cmake --build "${pkgname}/build" -v
}

package() {
    DESTDIR="$pkgdir" cmake --install "${pkgname}/build"
}
