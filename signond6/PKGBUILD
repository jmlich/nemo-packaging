# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=signond6
pkgver=8.61
pkgrel=1
pkgdesc='A D-Bus service which performs user authentication on behalf of its clients'
arch=('x86_64' 'aarch64')
url='https://gitlab.com/accounts-sso/signond/'
license=(LGPL)
depends=(qt6-base)
makedepends=(doxygen graphviz ttf-font qt6-tools)
source=("https://gitlab.com/accounts-sso/signond/-/archive/VERSION_$pkgver/signond-VERSION_$pkgver.tar.bz2"
    '0001-allow_build_with_qt6.patch')
sha256sums=('5d9f29fbf5892d01e38ce931b9a69c4bb08e6788f6cbdc80c277f4723b44249d'
    'e43c6b7e29ce458914fc8aebc8c0ff5f239a1b6dec8b3d185e9ee4f3d25ba664')

prepare() {
  cd signond-VERSION_$pkgver
  patch -p1 --input="${srcdir}/0001-allow_build_with_qt6.patch"
}

build() {
  cd signond-VERSION_$pkgver
  qmake6 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd signond-VERSION_$pkgver
  make INSTALL_ROOT="$pkgdir" install

  # Do not ship docs
  rm -rf "$pkgdir"/usr/share/doc
}
