# $Id$
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

_host="git.sailfishos.org"
_project=mer-core
_basename=buteo-sync-plugin-carddav
_branch=master

_gitname=$_basename
pkgname=$_basename-git

pkgver=0.1.6.r0.gb67977c

pkgrel=1
pkgdesc="Syncs contact data from CardDAV services"
arch=('x86_64' 'aarch64')
url="https://$_host/$_project/$_gitname"
license=('BSD-3-Clause')
depends=('libphonenumber' 'qt5-mlocale-git' 'libsailfishkeyprovider-git' 'buteo-syncfw-git')
makedepends=('git' 'qt5-tools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
    cd "${srcdir}/${pkgname}"
#    patch -p1 --input="${srcdir}/update-api-for-qtpim-5.9.patch"
}

build() {
  cd "${srcdir}/${pkgname}"
  # Not possible to install in build subdir
  qmake-qt5
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make INSTALL_ROOT="${pkgdir}" install
  # Remove installed tests
  rm -r "$pkgdir"/opt
}
