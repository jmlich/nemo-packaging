# Contributer Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libdsme
pkgver=0.66.6
pkgrel=1
pkgdesc="DSME dsmesock dynamic library"
arch=('x86_64')
url="https://git.merproject.org/mer-core/libdsme"
license=('LGPL')
depends=('glib2')
makedepends=('git' 'check')
source=("git+https://git.merproject.org/mer-core/libdsme.git#tag=$pkgver" libdsme-uio.patch)
sha512sums=('SKIP'
            '342b2343d622b180a7e1d52b0827f2eccd0cce23d650962889cbfe579514c02a2d0fec82f26b0bb3bd91cc8f22ce6717388686948987fa4b035d5cc5b0e1ea64')

prepare() {
  cd libdsme
  patch -p1 -i ../libdsme-uio.patch # Fix build
}

build() {
  cd libdsme
  ./verify_version
  make
}

package() {
  cd libdsme
  make DESTDIR="${pkgdir}" install_main install_devel

  # Fix symlinks
  for name in libdsme libdsme_dbus_if libthermalmanager_dbus_if; do
    ln -sf $name.so.0.3.0 "$pkgdir"/usr/lib/$name.so.0
    ln -sf $name.so.0.3.0 "$pkgdir"/usr/lib/$name.so
  done
}