# Contributer Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libdsme
pkgver=0.66.7
pkgrel=1
pkgdesc="DSME dsmesock dynamic library"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/libdsme"
license=('LGPL')
depends=('glib2')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
'0001-fix-use-after-free.patch')
sha256sums=('95b6ccb7db39db8bb07e01777f4c0055e4a597c5100313329700995c0a84d87e'
    '54e1c83ca7e1ab07381d0867ddaa4e55b31ad5163af08f95c66f4be92ee03426')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 --input="${srcdir}/0001-fix-use-after-free.patch"
}

build() {
  cd $pkgname-$pkgver
  ./verify_version
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install_main install_devel

  # Fix symlinks
  for name in libdsme libdsme_dbus_if libthermalmanager_dbus_if; do
    ln -sf $name.so.0.3.0 "$pkgdir"/usr/lib/$name.so.0
    ln -sf $name.so.0.3.0 "$pkgdir"/usr/lib/$name.so
  done
}
