# Maintainer: Bernhard Landauer <bernhard@manjaro.org>
# Contributor: Bhushan Shah <bshah@kde.org>

pkgname=maliit-framework
pkgver=2.3.0
pkgrel=2
pkgdesc="Framework for Maliit"
arch=('aarch64' 'x86_64')
url="https://github.com/maliit/framework"
license=('LGPL')
depends=('qt5-declarative' 'wayland-protocols' 'qt5-wayland')
makedepends=('cmake' 'doxygen' 'graphviz')
source=("${url}/archive/refs/tags/${pkgver}.tar.gz"
	"581adc21b3cfd87aa13fa7cf2e9574c1111fa50b.patch"
	"c787e02f7354f5310b0706b2e8eaee6f3617d9af.patch"
	"3fef6c2069e890c39c296a31bbf3faf2ecbd67e8.patch")
sha256sums=('bfc23919ac8b960243f85e8228ad7dfc28d557b52182a0b5a2a216a5c6a8057c'
	"c41777b102144f8cabee70d7dccbca28e06389f0957e6f18efd72bc4b9b34715"
	"4e1831bcea54a09bafcb8b4e0a0d5ea68df7ae4c8ae72c92beba7934f224bbf8"
	"21ac71f019cfed42b9970334b6c28d0ca8578806ec3784f7ad7bb550529419c3")

prepare() {
    cd framework-$pkgver
    patch -Np1 -i "${srcdir}/581adc21b3cfd87aa13fa7cf2e9574c1111fa50b.patch"
    patch -Np1 -i "${srcdir}/c787e02f7354f5310b0706b2e8eaee6f3617d9af.patch"
    patch -Np1 -i "${srcdir}/3fef6c2069e890c39c296a31bbf3faf2ecbd67e8.patch"
    mkdir -p build
}

build() {
    cd framework-$pkgver/build
    cmake ../ \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQT5_PLUGINS_INSTALL_DIR=lib/qt/plugins \
    -DQT5_MKSPECS_INSTALL_DIR=lib/qt/mkspecs \
    -Denable-docs=OFF \
    -Denable-tests=OFF \
    -DCMAKE_BUILD_TYPE=Release
    make
}

package() {
    cd framework-$pkgver/build
    make DESTDIR="$pkgdir/" install
}
