# $Id$
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

_host="git.sailfishos.org"
_project=mer-core
_basename=contacts
_branch=master

_gitname=lib${_basename}
pkgname=qt5-$_basename-git

pkgver=0.2.8

pkgrel=1
pkgdesc="A contact cache and various helper functions"
arch=('x86_64' 'aarch64')
url="https://$_host/$_project/$_gitname#tag=$pkgver"
license=('BSD-3-Clause')
depends=('qt5-declarative' 'qt5-pim-git' 'qt5-mlite-git' 'qt5-mlocale-git' 'qt5-mce-git' 'libphonenumber' 'qt5-contacts-sqlite-extensions-git')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}" "update-api-for-qtpim-5.9-v2.patch")
md5sums=('SKIP' 'SKIP')

prepare() {
    cd "${srcdir}/${pkgname}"
    patch -p1 --input="${srcdir}/update-api-for-qtpim-5.9-v2.patch"
}

build() {
    cd "${srcdir}/${pkgname}"
    qmake-qt5 PREFIX=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="$pkgdir/" install
    
    # Remove installed tests
    rm -r "$pkgdir"/opt
}
 
