# $Id$

pkgname=glacier-settings-developermode-git
pkgver=0.0.3.r0.g37fcdc5
pkgrel=1
pkgdesc="developermode settings plugin for glacier-settings"
arch=('x86_64' 'aarch64')
url="https://github.com/nemomobile-ux/glacier-settings-developermode/#branch=master"
license=('BSD-3-Clause')
depends=('glacier-settings'
)

makedepends=('git' 'qt5-tools' 'cmake')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
    cmake \
        -B "${pkgname}/build" \
        -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr'
    make -C "${pkgname}/build" all
}

package() {
    make -C "${srcdir}/${pkgname}/build" DESTDIR="$pkgdir" install
}
