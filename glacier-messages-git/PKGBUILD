# $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=glacier-messages-git
pkgver=0.2.8.r0.gfc0f43f
pkgrel=1
pkgdesc="QML based messages application for nemomobile"
arch=('x86_64' 'aarch64')
url="https://github.com/nemomobile-ux/glacier-messages"
license=('BSD-3-Clause')
depends=('qt6-glacier-app'
        'nemo-qml-plugin-contacts'
        'nemo-qml-plugin-notifications'
        'nemo-qml-plugin-dbus'
        'libcommhistory'
        'libqofono-qt6'
        'glacier-contacts-git')
makedepends=('git' 'cmake' 'qt6-tools' 'clang')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
    cmake \
        -B "${pkgname}/build" \
        -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr'
    make -C "${pkgname}/build" all
}

package() {
    make -C "${srcdir}/${pkgname}/build" DESTDIR="$pkgdir" install

    mkdir -p ${pkgdir}/usr/share/themes/glacier/meegotouch/icons/
    cd ${pkgdir}/usr/share/themes/glacier/meegotouch/icons/
    ln -s /usr/share/glacier-messages/glacier-messages.png icon-lock-sms.png
}
