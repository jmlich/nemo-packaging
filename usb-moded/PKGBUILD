## $Id$
# Maintainer: TheKit <nekit1000 at gmail.com>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=usb-moded
pkgver=0.86.0+mer56
pkgrel=1
pkgdesc="Mer USB mode controller"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/usb-moded"
license=('GPL')
depends=('libdsme' 'dbus-glib' 'udev' 'kmod' 'sailfish-access-control' 'dhcp' 'net-tools')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
	'dhcpd.conf'
	'0001-use_etc-hw-release.patch')
sha256sums=('8776a4a373bc0ec375b22b28b2fb31e114bdc73aed911f5075ce11de2385847c'
	'a2408b6692339595f98fd5e46398da794019b4818ee865fd22953576f26366c9'
	'7c2d178094394e24dc08cc10213d9153b44ecab7994287d18be257e8fbd53305')

prepare() {
    cd $pkgname-0.86.0-mer56
    patch -p1 --input="${srcdir}/0001-use_etc-hw-release.patch"
    rm -rf dbus-gmain
    git clone https://github.com/sailfishos-mirror/dbus-glib.git dbus-gmain
    cd dbus-gmain
    git reset --hard d42176ae4763e5288ef37ea314fe58387faf2005
    cd ../
}

build() {
    cd $pkgname-0.86.0-mer56
    ./autogen.sh
    ./configure --prefix=/usr \
	--enable-app-sync \
	--enable-meegodevlock \
	--enable-debug \
	--enable-connman \
	--enable-systemd \
	--enable-sailfish-access-control
    make
}

package() {
	cd $pkgname-0.86.0-mer56
	make DESTDIR="$pkgdir/" install
	mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
#installs dev files
	install -m 644 -D src/usb_moded-dbus.h "$pkgdir/usr/include/$pkgname/usb_moded-dbus.h"
	install -m 644 -D src/usb_moded-modes.h "$pkgdir/usr/include/$pkgname/usb_moded-modes.h"
	install -m 644 -D src/usb_moded-appsync-dbus.h "$pkgdir/usr/include/$pkgname/usb_moded-appsync-dbus.h"
	install -m 644 -D src/com.meego.usb_moded.xml "$pkgdir/usr/include/$pkgname/com.meego.usb_moded.xml"
	install -m 644 -D usb_moded.pc "$pkgdir/usr/lib/pkgconfig/usb_moded.pc"
#install configs
	install -d $pkgdir/etc
	install -d $pkgdir/etc/usb-moded
	install -d $pkgdir/etc/usb-moded/run
	install -d $pkgdir/etc/usb-moded/run-diag
	install -d $pkgdir/etc/usb-moded/dyn-modes
	install -d $pkgdir/etc/usb-moded/diag
	install -m 644 -D config/dyn-modes/* $pkgdir/etc/usb-moded/dyn-modes/
	install -m 644 -D config/diag/* $pkgdir/etc/usb-moded/diag/
	install -m 644 -D config/run/* $pkgdir/etc/usb-moded/run/
	install -m 644 -D config/run-diag/* $pkgdir/etc/usb-moded/run-diag/
	install -m 644 -D config/mass-storage-jolla.ini $pkgdir/etc/usb-moded/
	install -m 644 -D config/10-usb-moded-defaults.ini $pkgdir/etc/usb-moded/
	install -m 644 -D "$srcdir"/dhcpd.conf "$pkgdir"/etc/usb-moded/

#install systemd stuff
	mkdir -p $pkgdir/usr/lib/systemd/system/basic.target.wants/
	mkdir -p $pkgdir/var/lib/environment/usb-moded/
	install -m 644 -D systemd/usb-moded-args.conf $pkgdir/var/lib/environment/usb-moded/usb-moded-args.conf
	install -m 755 -D systemd/turn-usb-rescue-mode-off $pkgdir/usr/bin/turn-usb-rescue-mode-off
	install -m 644 -D systemd/usb-rescue-mode-off.service $pkgdir/usr/lib/systemd/system/usb-rescue-mode-off.service
	install -m 644 -D systemd/usb-rescue-mode-off.service $pkgdir/usr/lib/systemd/system/graphical.target.wants/usb-rescue-mode-off.service
	install -m 644 -D systemd/usb-moded.conf $pkgdir/etc/tmpfiles.d/usb-moded.conf
	install -m 644 -D systemd/adbd-prepare.service $pkgdir/usr/lib/systemd/system/adbd-prepare.service
	install -m 644 -D systemd/adbd-prepare.service $pkgdir/usr/lib/systemd/system/graphical.target.wants/adbd-prepare.service
	mkdir -p $pkgdir $pkgdir/usr/bin
	install -m 744 -D systemd/adbd-functionfs.sh $pkgdir/usr/bin/adbd-functionfs.sh
	install -d $pkgdir/usr/share/user-managerd/remove.d/
	install -m 744 -D scripts/usb_mode_user_clear.sh $pkgdir/usr/share/user-managerd/remove.d/

	install -m 644 -D systemd/usb-moded.service $pkgdir/usr/lib/systemd/system/
	ln -s ../usb-moded.service $pkgdir/usr/lib/systemd/system/basic.target.wants/


	mkdir -p $pkgdir/etc/modprobe.d
	install -m 644 -D rpm/usb_moded.conf $pkgdir/etc/modprobe.d/usb_moded.conf

	mkdir -p $pkgdir/usr/share/dbus-1/system.d/
	install -m 644 -D debian/usb_moded.conf $pkgdir/usr/share/dbus-1/system.d/usb_moded.conf
}
