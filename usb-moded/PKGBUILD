## $Id$
# Maintainer: TheKit <nekit1000 at gmail.com>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

_basever=0.86.0
_merver=62

pkgname=usb-moded
pkgver=$_basever+mer$_merver
pkgrel=2
pkgdesc="Mer USB mode controller"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/usb-moded"
license=('GPL')
depends=('dbus-glib'
    'udev'
    'kmod'
    'sailfish-access-control'
    'dhcp'
    'net-tools')
optdepends=('buteo-mtp: mtp sync support')
source=("${url}/archive/refs/tags/mer/$pkgver.tar.gz"
	'https://github.com/sailfishos-mirror/dbus-glib/archive/d42176ae4763e5288ef37ea314fe58387faf2005.tar.gz'
	'dhcpd.conf'
	'0001-use_etc-hw-release.patch')
sha256sums=('661d8a383191482fd75a6dbf691d261747628fa39c79a28db68cefd37a3419f6'
	'f4c28d4740ac90863082e81c869e5178d25238b179747984faf0509e40d1afef'
	'a2408b6692339595f98fd5e46398da794019b4818ee865fd22953576f26366c9'
	'e340d6a21b94518ebb16b6ab76b0a12f197e5b5ae0861c1c46201f863a3b971f')

prepare() {
    cd $pkgname-mer-$_basever-mer$_merver
    patch -p1 --input="${srcdir}/0001-use_etc-hw-release.patch"
    cp -r ../dbus-glib-d42176ae4763e5288ef37ea314fe58387faf2005/* dbus-gmain/
}

build() {
    cd $pkgname-mer-$_basever-mer$_merver
    ./autogen.sh
    ./configure --prefix=/usr \
	--enable-app-sync \
	--enable-meegodevlock \
	--enable-debug \
	--enable-connman \
	--enable-systemd \
	--enable-sailfish-access-control
    make
}

package() {
	cd $pkgname-mer-$_basever-mer$_merver
	make DESTDIR="$pkgdir/" install
	mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
#installs dev files
	install -m 644 -D src/usb_moded-dbus.h "$pkgdir/usr/include/$pkgname/usb_moded-dbus.h"
	install -m 644 -D src/usb_moded-modes.h "$pkgdir/usr/include/$pkgname/usb_moded-modes.h"
	install -m 644 -D src/usb_moded-appsync-dbus.h "$pkgdir/usr/include/$pkgname/usb_moded-appsync-dbus.h"
	install -m 644 -D src/com.meego.usb_moded.xml "$pkgdir/usr/include/$pkgname/com.meego.usb_moded.xml"
	install -m 644 -D usb_moded.pc "$pkgdir/usr/lib/pkgconfig/usb_moded.pc"
#install configs
	install -d $pkgdir/etc
	install -d $pkgdir/etc/usb-moded
	install -d $pkgdir/etc/usb-moded/run
	install -d $pkgdir/etc/usb-moded/run-diag
	install -d $pkgdir/etc/usb-moded/dyn-modes
	install -d $pkgdir/etc/usb-moded/diag
	install -d $pkgdir/etc/tmpfiles.d
	install -m 644 -D "$srcdir"/dhcpd.conf "$pkgdir"/etc/usb-moded/
	install -m 644 -D config/10-usb-moded-defaults.ini "$pkgdir"/etc/usb-moded/
	install -m 644 -D systemd/usb-moded.conf $pkgdir/etc/tmpfiles.d/

#install systemd stuff
	mkdir -p $pkgdir/usr/lib/systemd/system/basic.target.wants/
	install -m 644 -D systemd/usb-moded.service $pkgdir/usr/lib/systemd/system/
	ln -s ../usb-moded.service $pkgdir/usr/lib/systemd/system/basic.target.wants/

	mkdir -p $pkgdir/etc/modprobe.d
	install -m 644 -D rpm/usb_moded.conf $pkgdir/etc/modprobe.d/usb_moded.conf

	mkdir -p $pkgdir/usr/share/dbus-1/system.d/
	install -m 644 -D debian/usb_moded.conf $pkgdir/usr/share/dbus-1/system.d/usb_moded.conf
}
