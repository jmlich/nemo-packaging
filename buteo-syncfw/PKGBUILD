# $Id$
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=buteo-syncfw
pkgver=0.11.1
pkgrel=1
pkgdesc="Synchronization backend"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/buteo-syncfw"
license=('LGPLv2')
depends=('libiphb' 'nemo-keepalive' 'libmce-qt' 'signond' 'libaccounts-qt')
makedepends=('qt5-tools' 'doxygen')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
	"msyncd.service")
sha256sums=('0864c0fdab39bb985b62d3b2f302e21ef0b7cb9ff8bf38e8824f39b9c27ecd92'
	'bd37f105cd64ebac3dc2c991bfc8f7b989c8311800b92dd8e50add996aebc133')

build() {
  cd $pkgname-$pkgver
  qmake-qt5 CONFIG+=usb-moded DEFINES+=USE_KEEPALIVE
  make
}

package() {
  cd $pkgname-$pkgver
  make -j 1 INSTALL_ROOT="${pkgdir}" install

  rm "${pkgdir}/usr/lib/systemd/user/msyncd.service"
  cp ${srcdir}/msyncd.service "${pkgdir}/usr/lib/systemd/user/msyncd.service"

  mkdir -p ${pkgdir}/usr/lib/systemd/user/graphical-session.target.wants/
  ln -s ../msyncd.service ${pkgdir}/usr/lib/systemd/user/graphical-session.target.wants/

  # Remove installed tests
  rm -r "$pkgdir"/opt
}
