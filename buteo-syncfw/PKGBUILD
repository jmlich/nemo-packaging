# $Id$
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

_host="git.sailfishos.org"
_project=mer-core
_basename=buteo-syncfw
_branch=master

_gitname=$_basename
pkgname=$_basename-git

pkgver=0.10.3.r0.g1a50ddb

pkgrel=1
pkgdesc="Synchronization backend"
arch=('x86_64' 'aarch64')
url="https://$_host/$_project/$_gitname"
license=('LGPLv2')
depends=('libiphb-git' 'nemo-keepalive-git' 'mapplauncherd-qt-git' 'qt5-mce-git' 'signond' 'libaccounts-qt')
makedepends=('git' 'qt5-tools' 'doxygen')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
    cd "${srcdir}/${pkgname}"
}

build() {
  cd "${srcdir}/${pkgname}"
  qmake-qt5 CONFIG+=usb-moded DEFINES+=USE_KEEPALIVE
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make INSTALL_ROOT="${pkgdir}" install
  # Remove installed tests
  rm -r "$pkgdir"/opt
}
