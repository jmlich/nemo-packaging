# $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=libiodata
pkgver=0.19.13
pkgrel=1
pkgdesc="Mer library for input/output data"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/libiodata"
license=('LGPL-2.1-or-later')
depends=('qt6-base')
makedepends=('bison' 'flex')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
    '0001-Fixup-qt6-build.patch'
    '0002-Fixup-patches.patch'
)
sha256sums=('56e18738992d55893434052a9b8e5e60f6731e3daa932afcb1ec0776eda0d01a'
    '87b5ae0f6db982529d6122bda7f1eb6ccd39f37fdaab0d56b65a3897007ee3d4'
    '0cf0f48a70bde6b9adf428ea784517dc290dfc69ce01483fecef91187a89ef91'
)


prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 --input="${srcdir}/0001-Fixup-qt6-build.patch"
  patch -p1 --input="${srcdir}/0002-Fixup-patches.patch"
  # Logging macro cause compilation to fail otherwise
  sed -i "s@define LOG_LEVEL LOG_WARNING@define LOG_LEVEL LOG_NONE@" src/log.h
}

build() {
  cd "${pkgname}-${pkgver}"
  export IODATA_VERSION=${pkgver}
  qmake6
  make -j1
}

package() {
  cd "${pkgname}-${pkgver}"
  make -j 1 INSTALL_ROOT="${pkgdir}" install
}
