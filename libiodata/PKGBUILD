# $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=libiodata
pkgver=0.19.12
pkgrel=1
pkgdesc="Mer library for input/output data"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/libiodata"
license=('LGPL-2.1-or-later')
depends=('qt6-base')
makedepends=('bison' 'flex')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
    '0001-Fixup-qt6-build.patch'
    '0002-install-prf-file-into-usr-lib64-qt6-features-mkspecs.patch'
)
sha256sums=('30e062e488cee10f1b991de2b5c0929ffeec07048564dcfe0c552dc820d77bc2'
    'e731c59d31afea941bc0bc10aa354ce0974ddbdb9c2043cefbbd5aea3c2d1fbe'
    'f4339880ad5c8159f5c73894288663236bc6d9425384c384a7a4a54d6a78f1cf'
)


prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 --input="${srcdir}/0001-Fixup-qt6-build.patch"
  patch -p1 --input="${srcdir}/0002-install-prf-file-into-usr-lib64-qt6-features-mkspecs.patch"
  # Logging macro cause compilation to fail otherwise
  sed -i "s@define LOG_LEVEL LOG_WARNING@define LOG_LEVEL LOG_NONE@" src/log.h
}

build() {
  cd "${pkgname}-${pkgver}"
  export IODATA_VERSION=${pkgver}
  qmake6
  make -j1
}

package() {
  cd "${pkgname}-${pkgver}"
  make -j 1 INSTALL_ROOT="${pkgdir}" install
}
