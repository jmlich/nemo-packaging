# $Id$

pkgname=pure-maps-git
pkgver=3.1.0.r3.ga62de4a8
pkgrel=1
pkgdesc="Pure Maps"
arch=('x86_64' 'aarch64')
url="https://github.com/rinigus/pure-maps"
license=('GPL-3.0 License')
depends=(
    'python-pyotherside'
    'python-pyxdg'
    'maplibre-gl-native'
    'mapbox-gl-qml'
    's2geometry'
    'nemo-qml-plugin-dbus'
    'qt5-quickcontrols2'
)
makedepends=('git' 'cmake' 'qt5-tools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}#branch=master"
    'apikeys.py')
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
    cd "$srcdir/${pkgname}"
    git submodule update --init --recursive
}

build() {
    cp "$srcdir/apikeys.py" "${pkgname}/tools/"
    cmake \
        -B "${pkgname}/build" \
        -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DFLAVOR=qtcontrols
    make -C "${pkgname}/build" all
}

package() {
    make -C "${srcdir}/${pkgname}/build" DESTDIR="$pkgdir" install
}
