## $Id$
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=buteo-mtp
pkgver=0.9.5
pkgrel=1
pkgdesc="MTP library"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/$pkgname"
license=('BSD')
depends=('mlite'
    'nemo-qml-plugin-systemsettings'
    'nemo-qml-plugin-dbus'
    'nemo-qml-plugin-thumbnailer')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
    "0001-disable_ssu.patch")
sha256sums=('6e3d662911c67f593d2bb88b49170cb40dc3fd0c1b17482947761c4d9807e84e'
    'eb46ea60f45151893d210d3e5e2a9229cec7455f25e1ee278287bfd1eda433d3')

prepare() {
    cd $pkgname-$pkgver
    patch -p1 --input="${srcdir}/0001-disable_ssu.patch"
}

build() {
    cd $pkgname-$pkgver
    qmake
    make
}

package() {
    cd $pkgname-$pkgver
    make -j 1 INSTALL_ROOT="$pkgdir/" install
    rm -rf $pkgdir/opt
}
