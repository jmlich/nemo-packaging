# Maintainer: dodgejcr@gmail.com
# Maintainer: Bernhard Landauer <bernhard@manjaro.org>
# Contributor: Bhushan Shah <bshah@kde.org>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=libqofono-qt6
pkgver=0.122
pkgrel=1
pkgdesc="A library for accessing the ofono daemon, and a declarative plugin for it. This allows accessing ofono in qtquick and friends."
arch=('aarch64' 'x86_64')
url="https://github.com/sailfishos/libqofono"
license=('LGPL')
provides=('libqofono')
depends=('qt6-declarative'
    'ofono>=1.29')

source=("${url}/archive/refs/tags/$pkgver.tar.gz"
    "context-preferred.patch"
    "mtk_settings_binding.patch"
    '0003-fix-invalid-xml.patch'
)

sha256sums=('7f4a3ecfca41c4549473c68cfe2f0fe4bfbb910b4a7a288799789018ae0e1fe7'
    'f793347078553903c7e6f6b235091c1863b18cd9b4078f34addc7a3ded7f2bd8'
    '1f63b04d05be4988498cba96c9700d6f4b6237325d65e508cf6fa2f21247a765'
    '96bf3f083de016a35fdf68a304093c7e096819de635db144e4c857ad87afa21f'
)

prepare() {
    cd libqofono-$pkgver
    mkdir -p build

    # None of these patches break any abi or api, they only add qml functions
    # nor does these patches change the code, only adds code

    # Not upstreamed patches
    patch -Np1 -i "${srcdir}/context-preferred.patch"
    patch -Np1 -i "${srcdir}/mtk_settings_binding.patch"
    patch -Np1 -i "${srcdir}/0003-fix-invalid-xml.patch"
}

build() {
    cd libqofono-$pkgver/build
    qmake6 PREFIX=/usr ../libqofono.pro
	make
}

package() {
    cd libqofono-$pkgver/build
    make -j 1 INSTALL_ROOT="$pkgdir/" install

    # move *.prf into right folder
    mkdir -p ${pkgdir}/usr/lib/qt6/mkspecs/features/
    mv ${pkgdir}/usr/share/qt6/mkspecs/features/qofono-qt6.prf ${pkgdir}/usr/lib/qt6/mkspecs/features/qofono-qt6.prf

    rm -r "$pkgdir/opt"
}
