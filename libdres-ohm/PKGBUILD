## $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=libdres-ohm
pkgver=1.1.18
pkgrel=1
pkgdesc="Dependency resolver for OHM"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/libdres-ohm"
license=('LGPL-2.1-only')
depends=('glib2'
    'ohm'
    'libtrace-ohm'
    'libprolog')

makedepends=('automake' 'autoconf')
source=("${url}/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('fd6abb2a19d5cb2e1b333b6bc31ae985e154f14f48f20d507a6959436f8df21e')

prepare() {
    cd $pkgname-$pkgver
    ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
 
