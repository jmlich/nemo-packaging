# $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=timed
pkgver=3.6.20
pkgrel=2
pkgdesc="Mer time daemon"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/timed"
license=('LGPL-2.1-or-later')
depends=('sailfish-access-control' 'libiodata' 'tzdata-timed' 'connman' 'ofono' 'pcre')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
    "0001-Fix_qt6_build.patch"
    "0002-Fixes-access.patch"
    '0003-qt6-include.patch'
)

sha256sums=('413728c9224074b84fb37216fcd8b671de8557315fa1c4a7821517d2ab42b923'
	'a640eab5dba3af3be43e51784c00b6afb7b8e235f638b19a6ea2fbc32d9768e7'
	'cf81d073f2618fd914fbd5f55152580c7e3bf14f53eaf0950cb7998fd9460e02'
	'6bfb8a8fbe61f132b9c5a0ddc170aff0951dcb4902f80e402896d7358eb7b9cd'
)

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i "${srcdir}/0001-Fix_qt6_build.patch"
  patch -Np1 -i "${srcdir}/0002-Fixes-access.patch"
  patch -Np1 -i "${srcdir}/0003-qt6-include.patch"
  mkdir -p src/h/timed-qt6/
  cp src/lib/qmacro.h src/h/timed-qt6/qmacro.h
  sed -i "s|libsystemd-daemon|libsystemd|g" ./src/server/server.pro
}

build() {
  cd $pkgname-$pkgver
  export TIMED_VERSION=${pkgver}
  qmake6 -recursive "CONFIG += dsme_dbus_if ofono"
  make
}

package() {
  cd $pkgname-$pkgver
  make -j 1 INSTALL_ROOT="$pkgdir/" install
  sed -i 's/WantedBy=pre-user-session.target/WantedBy=graphical-session-pre.target/' "${pkgdir}/usr/lib/systemd/user/timed-qt6.service"

  # Remove tests
  rm -rf "$pkgdir/opt"
  #add some dir
  install -d -m 0777 ${pkgdir}/var/lib/timed
  touch ${pkgdir}/var/lib/timed/localtime
  chmod 666 ${pkgdir}/var/lib/timed/localtime
  install -d -m 0777 ${pkgdir}/var/lib/timed/shared_events
  install -d -m 0777 ${pkgdir}/var/lib/timed/shared_settings

  # Make /etc/localtime a link to /var/lib/timed/localtime to make system time zone follow timed.
  install -d ${pkgdir}/etc/
  ln -sf /var/lib/timed/localtime ${pkgdir}/etc/localtime

  install -d ${pkgdir}/usr/lib/qt6/mkspecs/features/
  mv ${pkgdir}/usr/share/qt6/mkspecs/features/timed-qt6.prf ${pkgdir}/usr/lib/qt6/mkspecs/features/timed-qt6.prf
  mv ${pkgdir}/usr/share/qt6/mkspecs/features/timed-voland-qt6.prf ${pkgdir}/usr/lib/qt6/mkspecs/features/timed-voland-qt6.prf

  #setup services
  install -d ${pkgdir}/usr/lib/systemd/user/user-session.target.wants/
  ln -s ../timed-qt6.service ${pkgdir}/usr/lib/systemd/user/user-session.target.wants/
}
