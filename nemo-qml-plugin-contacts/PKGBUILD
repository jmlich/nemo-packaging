# $Id$
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=nemo-qml-plugin-contacts
pkgver=0.3.26
pkgrel=3
pkgdesc="Nemo QML contacts plugin"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/nemo-qml-plugin-contacts"
license=('BSD-3-Clause')
depends=('libphonenumber-nemo'
	'libmlocale'
	'mce'
	'mce-headers'
	'qtcontacts-sqlite'
	'libaccounts-qt6'
	'buteo-sync-plugin-carddav'
	'icu')

makedepends=('qt6-tools')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
    "0001-fix_build_on_qt6.patch")
sha256sums=('b9f1d35f834e4860ee7eeae9b70a5d5fae78f21c5eb58b686972913f7836aedc'
    '230b4e3a3fc293e98fb207c6871560bd9fec424c93eb450d24c8b6bfbf88f500')

prepare() {
    cd "$srcdir/${pkgname}-${pkgver}"
    patch -p1 --input="${srcdir}/0001-fix_build_on_qt6.patch"
}

build() {
  cd $pkgname-$pkgver
  # Not possible to install in build subdir
  qmake6 VERSION=$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make -j 1 INSTALL_ROOT="${pkgdir}" install
  # Remove installed tests
  rm -r "$pkgdir"/opt
}
