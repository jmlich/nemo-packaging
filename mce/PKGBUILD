## $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=mce
pkgver=1.110.0
pkgrel=1
pkgdesc="Mode Control Entity for Nokia mobile computers"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/mce"
license=('LGPLv2')
depends=('libngf' 'libiphb')
makedepends=('git'
	'doxygen'
	'libdsme'
	'libsystemd'
	'usb-moded'
	'mce-headers>=1.30')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
	'0001-charger-Add-pinephone-charger-detection.-Fixes-JB-49.patch'
	'0002-Handle_KEY_WAKEUP.patch')
sha256sums=('a78933194cc0679a8eb8f8bc4fe7141eeeadd7bb126a4ec5ba715654f9446936'
	'8ed260853165760423b4c6764ae9fb129e0506403d070fced60f576d9185e5f4'
	'595ddbfda22a802a47f0239b24f53a28aa785abe128ae56cfbdbcac0d0deae2d')


prepare() {
    cd $pkgname-$pkgver
    sed -i "s|libsystemd-daemon|libsystemd|g" ./Makefile
    rm -rf dbus-gmain
    git clone https://github.com/sailfishos-mirror/dbus-glib.git dbus-gmain
    cd dbus-gmain
    git reset --hard d42176ae4763e5288ef37ea314fe58387faf2005
    cd ../
    patch -p1 --input="${srcdir}/0001-charger-Add-pinephone-charger-detection.-Fixes-JB-49.patch"
    patch -p1 --input="${srcdir}/0002-Handle_KEY_WAKEUP.patch"
}

build() {
    cd $pkgname-$pkgver
    ./verify_version
    make

}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir/" install ENABLE_MANPAGE_INSTALL=n

    mv $pkgdir/usr/sbin $pkgdir/usr/bin

    rm $pkgdir/lib/systemd/system/multi-user.target.wants/mce.service
    rmdir $pkgdir/lib/systemd/system/multi-user.target.wants
    mv $pkgdir/lib/systemd $pkgdir/usr/lib/systemd

    sed -i 's/Before=basic.target//' $pkgdir/usr/lib/systemd/system/mce.service

    mkdir -p $pkgdir/usr/lib/tmpfiles.d
    echo "d /var/run/mce 0755 root root -" > $pkgdir/usr/lib/tmpfiles.d/mce.conf
    rm -rf $pkgdir/lib
    rm -rf $pkgdir/var/run
}
