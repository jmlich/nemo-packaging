## $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=mce
pkgver=1.115.0
pkgrel=1
pkgdesc="Mode Control Entity for Nokia mobile computers"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/mce"
license=('LGPLv2')
depends=('libngf>=0.24'
	'libiphb'
)
makedepends=('doxygen'
	'libdsme'
	'libsystemd'
	'glib2'
	'usb-moded'
	'mce-headers>=1.32')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
	"https://github.com/sailfishos-mirror/dbus-glib/archive/d42176ae4763e5288ef37ea314fe58387faf2005.tar.gz"
	'0001-charger-Add-pinephone-charger-detection.-Fixes-JB-49.patch'
	'0002-Handle_KEY_WAKEUP.patch')
sha256sums=('4e312e071618ca786005bac19a105a9014b0439b43aae5532001780abe151b89'
	'f4c28d4740ac90863082e81c869e5178d25238b179747984faf0509e40d1afef'
	'8ed260853165760423b4c6764ae9fb129e0506403d070fced60f576d9185e5f4'
	'595ddbfda22a802a47f0239b24f53a28aa785abe128ae56cfbdbcac0d0deae2d')


prepare() {
    cd $pkgname-$pkgver
    sed -i "s|libsystemd-daemon|libsystemd|g" ./Makefile
    cp ../dbus-glib-d42176ae4763e5288ef37ea314fe58387faf2005/dbus-gmain.* dbus-gmain/
    patch -p1 --input="${srcdir}/0001-charger-Add-pinephone-charger-detection.-Fixes-JB-49.patch"
    patch -p1 --input="${srcdir}/0002-Handle_KEY_WAKEUP.patch"
}

build() {
    cd $pkgname-$pkgver
    ./verify_version
    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir/" install ENABLE_MANPAGE_INSTALL=n

    mv $pkgdir/usr/sbin $pkgdir/usr/bin

    rm $pkgdir/lib/systemd/system/multi-user.target.wants/mce.service
    rmdir $pkgdir/lib/systemd/system/multi-user.target.wants
    mv $pkgdir/lib/systemd $pkgdir/usr/lib/systemd

    sed -i 's/Before=basic.target//' $pkgdir/usr/lib/systemd/system/mce.service

    mkdir -p $pkgdir/usr/lib/tmpfiles.d
    echo "d /var/run/mce 0755 root root -" > $pkgdir/usr/lib/tmpfiles.d/mce.conf
    rm -rf $pkgdir/lib
    rm -rf $pkgdir/var/run
}
