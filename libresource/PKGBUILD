# $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=libresource
pkgver=0.25.2
pkgrel=1
pkgdesc="MeeGo resource management low level C API libraries"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/libresource"
license=('LGPL-2.1-only')
depends=('dbus-glib' 'check')
makedepends=('automake' 'autoconf' 'libtool' 'git')
source=("${url}/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('1bbc00ca839830a46bee198220ec6889e6bd4bdce2cc9623e4207c4a3e75f258')

prepare() {
    cd $pkgname-$pkgver

    rm -rf dbus-gmain
    git clone https://github.com/sailfishos-mirror/dbus-glib.git dbus-gmain
    cd dbus-gmain
    git reset --hard d42176ae4763e5288ef37ea314fe58387faf2005
    cd ../

    echo "$pkgver" > .tarball-version
    ./autogen.sh --disable-static
}

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr --disable-static
    make -j1 #FIXME: For some reason it breaks otherwise
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir/" install
    # Remove tests
    rm -rf "$pkgdir"/opt
}
