# $Id$

pkgname=s2geometry
pkgver=0.10.0
pkgrel=1
pkgdesc="S2 is primarily designed to work with spherical geometry,"
arch=('x86_64' 'aarch64')
url="https://github.com/google/s2geometry"
license=('Apache-2.0 License')
depends=('abseil-cpp' 'openssl' 'gtest')
makedepends=('git' 'cmake')
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz"
    'fix-newer-openssl.patch'
    'cxxstd.patch'
)
sha256sums=(
    '1c17b04f1ea20ed09a67a83151ddd5d8529716f509dde49a8190618d70532a3d'
    '92a8a692282cf23f68274c8beef73186a876bde4f97a2ddd12c8b9669830cdbc'
    'beade6eba912ca130548f95014d0cf0b5b7219d4de2d11a7cbe392a00dc3b676'
)

prepare() {
    cd "$srcdir/${pkgname}-${pkgver}"
    patch -p1 --input="${srcdir}/fix-newer-openssl.patch"
    patch -p1 --input="${srcdir}/cxxstd.patch"
}

build() {
    cmake \
        -B "${pkgname}-${pkgver}/build" \
        -S "${pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DCMAKE_BUILD_TYPE=None \
        -DBUILD_SHARED_LIBS=ON \
        -DBUILD_EXAMPLES=OFF \
        -DCMAKE_VERBOSE_MAKEFILE=ON
    make -C "${pkgname}-${pkgver}/build" all
}

package() {
    make -C "${srcdir}/${pkgname}-${pkgver}/build" DESTDIR="$pkgdir" install
}
