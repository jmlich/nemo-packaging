# $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=nemo-qml-plugin-thumbnailer
pkgver=1.0.6
pkgrel=1
pkgdesc="Thumbnail provider plugin for Nemo Mobile"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/nemo-qml-plugin-thumbnailer"
license=('BSD-3-Clause')
depends=('qt5-declarative' 'mlite')
source=("${url}/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('6cdc3a998652bf11b6683392e45ab9abe8a8b752e2a818e1bb28b977fa2adafd')

build() {
  cd $pkgname-$pkgver
  mkdir -p build
  cd build
  qmake ..
  make
}

package() {
  cd $pkgname-$pkgver/build
  make -j 1 INSTALL_ROOT="${pkgdir}" install

  mkdir -p ${pkgdir}/usr/lib/qt/qml/org/nemomobile/thumbnailer/
  ln -sf /usr/lib/qt/qml/Nemo/Thumbnailer/libnemothumbnailer.so ${pkgdir}/usr/lib/qt/qml/org/nemomobile/thumbnailer/
  sed 's/Nemo.Thumbnailer/org.nemomobile.thumbnailer/' < ${pkgdir}/usr/lib/qt/qml/Nemo/Thumbnailer/qmldir > ${pkgdir}/usr/lib/qt/qml/org/nemomobile/thumbnailer/qmldir
}
