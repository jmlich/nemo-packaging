# $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

_basename=glacier-gallery
pkgname=${_basename}-git
pkgver=0.4.r0.ga919ddc
pkgrel=1
pkgdesc="The Glacier image gallery"
arch=('x86_64' 'aarch64')
url="https://github.com/nemomobile-ux/${_basename}"
license=('BSD-3-Clause')
depends=('qt6-glacier-app'
        'nemo-qml-plugin-settings'
        'nemo-qml-plugin-folderlistmodel'
        'nemo-qml-plugin-models'
        'nemo-qml-plugin-thumbnailer'
        'libresourceqt>=1.32.0'
        'qtdocgallery')
makedepends=('git' 'qt6-tools' 'clang' 'cmake')
provides=("${_basename}=${pkgver}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
    cmake \
        -B "${pkgname}/build" \
        -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr'
    cmake --build "${pkgname}/build" -v
}

package() {
    DESTDIR="$pkgdir" cmake --install "${pkgname}/build"
}
