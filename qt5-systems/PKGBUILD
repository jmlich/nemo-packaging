# $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=qt5-systems
pkgver=5.12
pkgrel=1
pkgdesc='Qt System modules'
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/qtsystems"
license=('GPL')
depends=('qt5-declarative' 'ofono' 'libmce-qt')
makedepends=('git' 'qt5-tools')
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

build() {
    cd ${srcdir}/${pkgname}
    qmake CONFIG+=ofono CONFIG+=battery_mce CONFIG-=bluez
    make
}

package() {
    cd ${srcdir}/${pkgname}
    make -j 1 INSTALL_ROOT="$pkgdir" install

    # Drop QMAKE_PRL_BUILD_DIR because it references the build dir
    find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;
}
