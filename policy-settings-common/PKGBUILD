## $Id$
# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

pkgname=policy-settings-common
pkgver=common.0.11.2.r0.g7a0e6fa
pkgrel=2
pkgdesc="Common policy settings"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/policy-settings-common"
license=('LGPL-2.1-only')
depends=('glib2' 'libprolog' 'swi-prolog7' 'libdres-ohm')
makedepends=('git' 'automake' 'autoconf')
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/\//./' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
    cd "${srcdir}/${pkgname}"
    ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
}
 
