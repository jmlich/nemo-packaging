# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qt6-sensors
_qtver=6.8.2
pkgver=${_qtver/-/}
pkgrel=1
arch=('x86_64' 'aarch64')
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Provides access to sensor hardware and motion gesture recognition'
depends=(qt6-connectivity)
makedepends=(cmake ninja qt6-declarative)
optdepends=('qt6-declarative: QML bindings'
            'iio-sensor-proxy: iio-sensor-proxy backend')
groups=(qt6)
_pkgfn=${pkgname/6-/}-everywhere-src-$_qtver
source=(https://download.qt.io/official_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz)
sha256sums=('8a817c9b001ad0abdbf15064826ca6a8ee6b66fef8f04b4343ac9f7b2ddbf4ca')

build() {
  cmake -B build -S $_pkgfn -G Ninja # -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
