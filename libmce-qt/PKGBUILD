## $Id$
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=libmce-qt
pkgver=1.5.0
pkgrel=1
pkgdesc="A library of Qt bindings for mce"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/libmce-qt"
license=('BSD-3-Clause')
depends=('qt6-declarative')
makedepends=('mce>=1.112' 'mce-headers')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
    '0001-Allow-build-with-qt6.patch'
)
sha256sums=('f05814a71551012edcc5fcd9d2861cbd2cb66f011ae61b5ff02b103fa01c74be'
    '7e39698e46841406d4cd896c91cda904dc1ec6a7ae2dded1acad07aede9d06e3')


prepare() {
  cd $pkgname-$pkgver
  patch -p1 --input="${srcdir}/0001-Allow-build-with-qt6.patch"
}



build() {
  cd $pkgname-$pkgver
  mkdir -p build
  cd build
  qmake6 ..
  make
}

package() {
  cd $pkgname-$pkgver
  cd build
  make -j 1 INSTALL_ROOT="${pkgdir}" install
}
