# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Florian Schweikert <kelvan@ist-total.org>

pkgname=python-pyotherside
_pyname=pyotherside
pkgver=1.6.0
pkgrel=1
pkgdesc='Asynchronous Python 3 Bindings for Qt6'
arch=('x86_64')
url='https://thp.io/2011/pyotherside'
license=('ISC')
depends=('python' 'qt6-base' 'qt6-declarative' 'qt6-svg')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/thp/${_pyname}/archive/${pkgver}.tar.gz")
sha256sums=('ead71d0a549acb6664d72745fae5b1d4e433274c64be6a680ceeeef0754451ab')

build() {
	cd "${_pyname}-${pkgver}/"

	qmake6
	make
}

package() {
	cd "${_pyname}-${pkgver}/"

	make INSTALL_ROOT="${pkgdir}/" install

	rm -r "${pkgdir}/usr/tests"
}
