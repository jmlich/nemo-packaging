# $Id$
# Contributor: TheKit <nekit1000 at gmail.com>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=nemo-qml-plugin-settings
pkgver=0.1.4
pkgrel=1
pkgdesc="Nemomobile QML wrapper for QSettings class"
arch=('x86_64' 'aarch64')
url="https://github.com/nemomobile-ux/nemo-qml-plugin-settings"
license=('LGPL-2.0-or-later')
depends=('qt6-declarative')
makedepends=('cmake' 'ninja')
source=("${url}/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('d1ab439d71cae94f271bffb21cd46cf65023985a2599bc56983f80367e1f4f63')

build() {
    cmake -B build -S $pkgname-$pkgver -G Ninja -DCMAKE_INSTALL_PREFIX:PATH='/usr'
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
