# Maintainer: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>

_host="github.com"
_project=nemomobile-ux
_basename=glacier-contacts
_branch=master

_gitname=$_basename
pkgname=$_basename-git

pkgver=0.9.r0.ga551505

pkgrel=1
pkgdesc="QML based contacts application for nemomobile"
arch=('x86_64' 'aarch64')
url="https://$_host/$_project/$_gitname#branch=$_branch"
license=('BSD-3-Clause')
makedepends=('git' 'cmake' 'qt6-tools' 'clang')
depends=('nemo-qml-plugin-thumbnailer'
        'nemo-qml-plugin-contacts'
        'nemo-qml-plugin-folderlistmodel'
        'glacier-gallery-git'
        'glacier-filemuncher-git'
        'nemo-qml-plugin-dbus'
        'qt6-glacier-app')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
    cmake \
        -B "${pkgname}/build" \
        -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr'
    make -C "${pkgname}/build" all
}

package() {
    make -C "${srcdir}/${pkgname}/build" DESTDIR="$pkgdir" install
}
