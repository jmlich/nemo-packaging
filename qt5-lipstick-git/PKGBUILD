## $Id$
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

_branch=master
pkgname=qt6-lipstick-git
pkgver=1.1.r1.g1c580f30
pkgrel=1
pkgdesc="QML toolkit for homescreen creation"
arch=('x86_64' 'aarch64')
url="https://github.com/nemomobile-ux/lipstick.git"
license=('LGPL-2.1-only')
depends=(
	    'qt6-sensors-sensorfw'
	    'qt6-wayland'
	    'nemo-keepalive'
	    'libresourceqt'
	    'libsystemd'
	    'mce-headers'
	    'libmce-qt>=1.5.0'
	    'libngf-qt6'
	    'nemo-qml-plugin-devicelock'
	    'nemo-qml-plugin-systemsettings'
	    'pulseaudio'
	    'pulseaudio-modules-nemo'
	    'pulseaudio-policy-enforcement'
	    'bluez-qt6')

makedepends=('git'
	'qt6-tools'
	'doxygen'
	'graphviz'
	'make'
	'pkgconfig'
	'timed')

provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}#branch=${_branch}")
sha256sums=('SKIP')


pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
  cd "${srcdir}/${pkgname}"
  git submodule init
  git submodule update
  mkdir -p build
  cd build
  cmake ../  -DCMAKE_INSTALL_PREFIX:PATH='/usr'
  make  all
}

package() {
  cd "${srcdir}/${pkgname}/build"
  make DESTDIR="$pkgdir" install
}
