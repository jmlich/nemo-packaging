## $Id$
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor: Chipligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgname=lipstick-glacier-home-git
_host="github.com"
_project=nemomobile-ux
_branch=master
pkgver=0.37.6.r2.g9262002
pkgrel=1
pkgdesc="A nice homescreen for Glacier experience"
arch=('x86_64' 'aarch64')
url="https://$_host/$_project/glacier-home"
license=('BSD-3-Clause AND MIT')
depends=('bluez-qt'
	'qt5-graphicaleffects'
	'qt5-multimedia'
	'qt5-feedback'
	'qtmpris'
	'qt5-lipstick-git'
	'qt5-quickcontrols-nemo-git'
	'nemo-qml-plugin-configuration'
	'nemo-qml-plugin-statusnotifier'
	'nemo-qml-plugin-time'
	'nemo-qml-plugin-contacts'
	'nemo-qml-plugin-notifications'
	'nemo-qml-plugin-connectivity'
	'glacier-settings-git'
	'mlite'
	'qt-mobility-haptics-ffmemless'
	'ngfd'
	'pulseaudio-module-keepalive')

makedepends=('git'
	'extra-cmake-modules'
	'qt5-wayland'
	'qt5-tools'
	'nemo-qml-plugin-devicelock'
	'bluez-qt'
	'polkit-qt5'
	'mce-headers>=1.30.0')
optdepends=('geoclue1')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
    cd "${srcdir}/${pkgname}"
}

build() {
  cd "${srcdir}/${pkgname}"
  cmake -B build \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc \
        -DUSE_GEOCLUE2=false
  make -C build
}

package() {
  cd "${srcdir}/${pkgname}"
  DESTDIR="$pkgdir" make -C build install
}
 
