## $Id$
# Contributor: Bart Ribbers <bribbers@disroot.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor: Chipligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>

_branch=master
pkgname=lipstick-glacier-home-git
pkgver=0.40.4.r2.gcc8199d
pkgrel=1
pkgdesc="A nice homescreen for Glacier experience"
arch=('x86_64' 'aarch64')
url="https://github.com/nemomobile-ux/glacier-home"
license=('BSD-3-Clause AND MIT')
depends=('qt6-multimedia'
	'qt6-feedback'
	'qtmpris'
	'qt6-lipstick'
	'qt6-quickcontrols-nemo'
	'nemo-qml-plugin-configuration'
	'nemo-qml-plugin-statusnotifier'
	'nemo-qml-plugin-time'
	'nemo-qml-plugin-contacts'
	'nemo-qml-plugin-notifications'
	'nemo-qml-plugin-connectivity'
	'glacier-settings'
	'geoclue1'
	'mlite6'
	'qt-mobility-haptics-ffmemless'
	'ngfd'
	'nemo-qml-plugin-devicelock'
	'bluez-qt>=6.0'
	'qt6-wayland'
	'polkit-qt6'
	'pulseaudio-module-keepalive'
	'glacier-settings-developermode')

makedepends=(
	'git'
	'extra-cmake-modules'
	'mce-headers>=1.30.0'
	'qt6-tools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}#branch=${_branch}")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
  cd "${srcdir}/${pkgname}"
  cmake -B build \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc \
        -DUSE_GEOCLUE2=false
  make -C build
}

package() {
  cd "${srcdir}/${pkgname}"
  DESTDIR="$pkgdir" make -C build install
}
