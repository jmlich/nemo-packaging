# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: Felix Yan <felixonm...@archlinux.org>
_gitname=mlite
pkgname=${_gitname}6
pkgver=0.5.3
pkgrel=1
pkgdesc="Useful classes originating from MeeGo Touch"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/mlite"
license=('LGPL')
depends=('qt6-base' 'dconf')
makedepends=('qt6-tools')
provides=("${_gitname}")
conflicts=("${_gitname}")
source=("${url}/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('1f2db95df805b30817f3f251a91d70c10292fdb4a2200a9f8514b8e38852d24e')

prepare() {
  cd ${_gitname}-$pkgver
  sed -i -e 's|/usr/libexec|/usr/lib|' \
      tools/mliteremoteaction/mliteremoteaction.pro \
      tools/mliteremoteaction/main.cpp \
      src/mremoteaction.cpp
}

build() {
  cd ${_gitname}-$pkgver

  qmake6 VERSION=${pkgver}
  make
}

package() {
  cd ${_gitname}-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  # Remove tests
  rm -r "$pkgdir/opt"
}
