# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: Felix Yan <felixonm...@archlinux.org>
_gitname=mlite
pkgname=${_gitname}6
pkgver=0.5.2
pkgrel=1
pkgdesc="Useful classes originating from MeeGo Touch"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/mlite"
license=('LGPL')
depends=('qt6-base' 'dconf')
makedepends=('qt6-tools')
provides=("${_gitname}")
conflicts=("${_gitname}")
source=("${url}/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('fea84dc132e1e33e7c65cbbab280dc3099e6dbf91a86c9ad23845a857da167da')

prepare() {
  cd ${_gitname}-$pkgver
  sed -i -e 's|/usr/libexec|/usr/lib|' \
      tools/mliteremoteaction/mliteremoteaction.pro \
      tools/mliteremoteaction/main.cpp \
      src/mremoteaction.cpp
}

build() {
  cd ${_gitname}-$pkgver

  qmake6 VERSION=${pkgver}
  make
}

package() {
  cd ${_gitname}-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  # Remove tests
  rm -r "$pkgdir/opt"
}
