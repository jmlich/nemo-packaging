# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: Felix Yan <felixonm...@archlinux.org>
_gitname=mlite
pkgname=${_gitname}6
pkgver=0.5.0
pkgrel=1
pkgdesc="Useful classes originating from MeeGo Touch"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/mlite"
license=('LGPL')
depends=('qt6-base' 'dconf')
makedepends=('qt6-tools')
provides=("${_gitname}")
conflicts=("${_gitname}")
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
    '0001-Fixup-build-on-qt6.patch'
    '0002-qt6-compatibility.patch'
)
sha256sums=('07870b878164c9bde9af8e9e9d7646d2e643442af37efacd798686a9be47e7c0'
    '2a191ec52f73699a1d77b9e4e346a49fb265b6040eba513d27a058be56af9efa'
    '26aa16bdd32ee54b0e882065b4aa04fff99a0200dd487c17f3ed9680bdb10245'
)

prepare() {
  cd ${_gitname}-$pkgver
  patch -p1 --input="${srcdir}/0001-Fixup-build-on-qt6.patch"
  patch -p1 --input="${srcdir}/0002-qt6-compatibility.patch"
  sed -i -e 's|/usr/libexec|/usr/lib|' \
      tools/mliteremoteaction/mliteremoteaction.pro \
      tools/mliteremoteaction/main.cpp \
      src/mremoteaction.cpp
}

build() {
  cd ${_gitname}-$pkgver

  qmake6 VERSION=${pkgver}
  make
}

package() {
  cd ${_gitname}-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  # Remove tests
  rm -r "$pkgdir/opt"
}
