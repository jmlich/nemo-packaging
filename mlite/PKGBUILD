# Contributor: Chupligin Sergey (NeoChapay) <neochapay@gmail.com>
# Maintainer: Felix Yan <felixonm...@archlinux.org>

pkgname=mlite
pkgver=0.4.1
pkgrel=2
pkgdesc="Useful classes originating from MeeGo Touch"
arch=('x86_64' 'aarch64')
url="https://github.com/sailfishos/mlite";
license=('LGPL')
depends=('qt6-base' 'dconf')
makedepends=('qt6-tools')
source=("${url}/archive/refs/tags/$pkgver.tar.gz"
    '0001-Fixup-build-on-qt6.patch'
    '0002-qt6-compatibility.patch'
)
sha256sums=('60c68c192e35d5278bcce3440cb736b3388e0192ad5f2aefbb56e0fc34359dc8'
    '2a191ec52f73699a1d77b9e4e346a49fb265b6040eba513d27a058be56af9efa'
    '26aa16bdd32ee54b0e882065b4aa04fff99a0200dd487c17f3ed9680bdb10245'
)

prepare() {
  cd $pkgname-$pkgver
  patch -p1 --input="${srcdir}/0001-Fixup-build-on-qt6.patch"
  patch -p1 --input="${srcdir}/0002-qt6-compatibility.patch"
  sed -i -e 's|/usr/libexec|/usr/lib|' \
      tools/mliteremoteaction/mliteremoteaction.pro \
      tools/mliteremoteaction/main.cpp \
      src/mremoteaction.cpp
}

build() {
  cd $pkgname-$pkgver

  qmake6 VERSION=${pkgver}
  make
}

package() {
  cd $pkgname-$pkgver

  make INSTALL_ROOT="$pkgdir" install

  # Remove tests
  rm -r "$pkgdir/opt"
}
