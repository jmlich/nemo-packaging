# $Id$
_basename=glacier-calendar
pkgname=${_basename}-git
pkgver=0.1.r0.g8fe191b
pkgrel=1
pkgdesc="Nemo calendar"
arch=('x86_64' 'aarch64')
url="https://github.com/nemomobile-ux/${_basename}"
license=('BSD-3-Clause' 'LGPL-2.1-only')
depends=('qt6-glacier-app' 'nemo-qml-plugin-calendar')
makedepends=('git' 'cmake' 'qt6-tools' 'clang')
provides=("${_basename}=${pkgver}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

build() {
    cmake \
        -B "${pkgname}/build" \
        -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr'
    make -C "${pkgname}/build" all
}

package() {
    make -C "${srcdir}/${pkgname}/build" DESTDIR="$pkgdir" install
}
